plugins{
    id 'java'
    id 'kr.entree.spigradle' version '2.2.4'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'io.freefair.lombok' version '6.1.0'
    id 'maven-publish'
}

group = 'com.github.AleIV'
version = '1.0-SNAPSHOT'

compileJava{
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}
javadoc {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
}


dependencies {
    // Paper
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    // ACF
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'

    // Model Engine
    compileOnly 'com.ticxo.modelengine:api:R2.2.0'

}

spigot {
    authors 'aleiv, Dolphln'
    apiVersion '1.16'
    version '0.1'
    name 'ModelTool'
    load STARTUP
    depends = ['ModelEngine']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from delombok
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

shadowJar {
    classifier = ''
    relocate 'co.aikar.commands', 'shadded.acf'
    relocate 'co.aikar.locales', 'shadded.acf.locales'
    minimize();

}

jar {
    classifier = "original"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.shadow.component(it)

            artifactId 'ModelTool'
            version version

            artifact jar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url 'http://repo.dolphln.com/artifactory/public/'
            allowInsecureProtocol true
            credentials {
                username dolphlnrepoUser
                password dolphlnrepoPass
            }
        }
    }
}

tasks.build.dependsOn tasks.shadowJar